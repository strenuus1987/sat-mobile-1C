
&НаКлиенте
Процедура СписокДобавить(Команда)
	
	МассивВыбранныхУслуг = Новый Массив;
	
	Для Каждого Строка Из ДопУслугиСписок Цикл 
		
		МассивВыбранныхУслуг.Добавить(Строка.ДопУслугаИД);
		
	КонецЦикла;
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыборДопУслуги", Новый Структура("ВыбранныеДопУслуги", МассивВыбранныхУслуг), ЭтаФорма,,,, Новый ОписаниеОповещения("СписокДобавитьОбработкаВыбораУслуги", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДобавитьОбработкаВыбораУслуги(Результат, ДопПараметры) Экспорт 
	
	Если Результат <> Неопределено Тогда
						
		Если Результат.Наименование = "Выезд за грузом к складу отправителя" 			
		ИЛИ Результат.Наименование = "Доставка груза к дверям получателя" Тогда
		
			ОткрытьФорму("ОбщаяФорма.ФормаВыборЗначенияДопУслуги", Новый Структура("ВыбраннаяУслуга", Результат.Наименование), ЭтаФорма,,,, Новый ОписаниеОповещения("СписокДобавитьОбработкаВыбораЗначения", ЭтаФорма, Новый Структура("РезультатНаименование, РезультатИД", Результат.Наименование, Результат.ИД)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
		ИначеЕсли Результат.Наименование = "Подъем на этаж ГО без лифта (каждое место до 25кг)" 
		ИЛИ Результат.Наименование = "Подъем на этаж ГО с лифтом (каждое место до 25 кг)" 
		ИЛИ Результат.Наименование = "Подъем на этаж ГП без лифта(каждое место до 25 кг)" 
		ИЛИ Результат.Наименование = "Подъем на этаж ГП с лифтом (каждое место до 25 кг)" 
		ИЛИ Результат.Наименование = "Укладка груза в гофроящик большой" 
		ИЛИ Результат.Наименование = "Укладка груза в гофроящик малый" 
		ИЛИ Результат.Наименование = "Укладка груза в гофроящик средний" 
		ИЛИ Результат.Наименование = "Укладка груза в п/п мешок со стяжкой" Тогда			
		
			ПоказатьВводЧисла(Новый ОписаниеОповещения("СписокДобавитьОбработкаВыбораЗначения", ЭтаФорма, Новый Структура("РезультатНаименование, РезультатИД", Результат.Наименование, Результат.ИД)), 1, "Сколько таких услуг Вам необходимо?", 10, 0);
				
		Иначе
			
			НоваяСтрока = ДопУслугиСписок.Добавить();
			НоваяСтрока.ДопУслуга = Результат.Наименование;
			НоваяСтрока.ДопУслугаИД = Результат.ИД;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДобавитьОбработкаВыбораЗначения(Результат, ДопПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		НоваяСтрока = ДопУслугиСписок.Добавить();
		НоваяСтрока.ДопУслуга = ДопПараметры.РезультатНаименование;
		НоваяСтрока.ДопУслугаИД = ДопПараметры.РезультатИД;
		
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
			НоваяСтрока.Значение = Результат.Наименование;
			НоваяСтрока.ЗначениеИД = Результат.ИД;
			
		Иначе
			
			НоваяСтрока.Значение = Результат;
			
		КонецЕсли;
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ДопУслугиСписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ДопУслуга", Элемент.ТекущиеДанные.ДопУслуга);
	ПараметрыОткрытия.Вставить("Значение", Элемент.ТекущиеДанные.Значение);
	ПараметрыОткрытия.Вставить("ДопУслугаИД", Элемент.ТекущиеДанные.ДопУслугаИД);
	ПараметрыОткрытия.Вставить("ЗначениеИД", Элемент.ТекущиеДанные.ЗначениеИД);
	
	ОткрытьФорму("ОбщаяФорма.ФормаРедактированияЗначенияДопУслуги", ПараметрыОткрытия, ЭтаФорма,,,, Новый ОписаниеОповещения("ДопУслугиСписокВыборЗавершение", ЭтаФорма, Новый Структура("ВыбраннаяСтрока", ВыбраннаяСтрока)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ДопУслугиСписокВыборЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		СтрокаТабл = ДопУслугиСписок.НайтиПоИдентификатору(ДопПараметры.ВыбраннаяСтрока);
		
		Если СтрокаТабл <> Неопределено Тогда
			
			Если Результат.Свойство("ЗначениеИД") Тогда
				СтрокаТабл.Значение = Результат.Значение;
				СтрокаТабл.ЗначениеИД = Результат.ЗначениеИД;
			Иначе
				СтрокаТабл.Значение = Результат.Значение;
			КонецЕсли;
					
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ДопУслугиСписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДопУслугиСписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДопУслугиСписок") Тогда
		
		Для Каждого Строка Из Параметры.ДопУслугиСписок Цикл
			
			НоваяСтрока = ДопУслугиСписок.Добавить();
			
			НоваяСтрока.ДопУслуга = Строка.ДопУслуга;
			НоваяСтрока.Значение = Строка.Значение;
			НоваяСтрока.ДопУслугаИД = Строка.ДопУслугаИД;
			НоваяСтрока.ЗначениеИД = Строка.ЗначениеИД;
						
		КонецЦикла;
			
	Иначе
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	СтруктураОткрытия = Новый Структура;
	
	Массив = Новый Массив;
	
	Для Каждого Строка Из ДопУслугиСписок Цикл
		
		Массив.Добавить(Новый Структура("ДопУслуга, Значение, ДопУслугаИД, ЗначениеИД", Строка.ДопУслуга, 
			Строка.Значение, Строка.ДопУслугаИД, Строка.ЗначениеИД));
		
	КонецЦикла;
	
	СтруктураОткрытия.Вставить("ДопУслугиСписок", Массив);
	
	ОповеститьОВыборе(СтруктураОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Закрыть();
	
КонецПроцедуры
